buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def VersionBuild = 0
if (System.getenv("BUILD_NUMBER") != null) {
    def buildver = System.getenv("BUILD_NUMBER")
    VersionBuild = "-${buildver}"
}

version = "${config.minecraft_version}-${config.mod_version}${VersionBuild}"
group = "foxie.peltier"
archivesBaseName = "Peltier"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"

    replaceIn "reference/Reference.java"
    replace "@VERSION@", "${config.mod_version}"

}

repositories {
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // the repo from which to get NEI and stuff
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.138:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.7.46:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.5.110:dev"
    compile "mcp.mobius.waila:Waila:1.5.10_1.7.10"
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
}

build.dependsOn deobfJar

// deployment
configurations { deployerJars }
dependencies { deployerJars "org.apache.maven.wagon:wagon-ssh:2.2" }

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: "scp://maven.foxiehost.lan/home/maven/public/") {
                authentication(userName: "maven", password: "maven")
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'Peltier'
                    url 'http://foxiemods.com/Peltier'
                    scm {
                        url 'https://github.com/CallMeFoxie/Peltier'
                        connection 'scm:git:git@github.com:CallMeFoxie/Peltier.git'
                        developerConnection 'scm:git:git@github.com:CallMeFoxie/Peltier.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/CallMeFoxie/Peltier/issues'
                    }
                    licenses {
                        license {
                            name 'TBD'
                            url 'https://does-not-exist'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'CallMeFoxie'
                            name 'CallMeFoxie'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
